<!DOCTYPE html>
<html lang="en">
	<head>
		<title>three.js webgl - loaders - MMD loader</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<style>
			body {
				background-color: black;
				margin: 0;
				padding: 0;
			}

			a { color: skyblue }

			canvas {
				display: block;
				position: absolute;
				top: 0;
				left: 0;
				right: 0;
				bottom: 0;
				margin: auto;
			}

			#info {
				color: white;
				font-size: 13px;
				position: absolute;
				bottom: 10px;
				width: 100%;
				text-align: center;
				z-index: 100;
			}
		</style>
	</head>

	<body>
		<div id="info">
		<a href="http://threejs.org" target="_blank">three.js</a> - webgl raymarching hybrid - live scene<br />
		Copyright
		<a href="http://www.geocities.jp/higuchuu4/index_e.htm" target="_blank">Model Data</a>
		<a href="http://www.nicovideo.jp/watch/sm13147122" target="_blank">Dance Data</a>
		</div>
		<script id="fragment_shader" type="x-shader/x-fragment">

#extension GL_EXT_frag_depth : require

precision highp float;

// uniforms
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

// camera
uniform vec3 cameraPosition;
uniform mat4 viewMatrix;

uniform mat4 cameraWorldMatrix;
uniform mat4 cameraProjectionMatrixInverse;
uniform float cameraNear;
uniform float cameraFar;

// consts
const float EPS = 0.05;
const float ROUGH_EPS = 0.3;
const float OFFSET = EPS * 100.0;
const float PI  = 3.14159265359;
const float PI2 = 6.28318530718;
const float GRID = 12.0;


// globals
const vec3 lightDir = vec3( -0.48666426339228763, 0.8111071056538127, -0.3244428422615251 );
vec3 cPos, cDir;

struct Intersect {
	bool isHit;

	vec3 position;
	float distance;
	vec3 normal;

	int material;
	vec3 color;
};

const int MIRROR_MATERIAL = 0;
const int FLOOR_MATERIAL = 1;
const int CIRCUIT_MATERIAL = 2;

// distance functions
vec3 onRep( vec3 p, float interval ) {
	vec2 q = mod( p.xz, interval ) - 0.5 * interval;
	return vec3( q.x, p.y, q.y );
}

vec3 onRepX( vec3 p, float interval ) {
	p.x = mod( p.x, interval ) - 0.5 * interval;
	return p;
}

mat2 rotate(in float a)
{
    float s=sin(a),c=cos(a);
    return mat2(c,s,-s,c);
}

vec2 fold(in vec2 p, in float s)
{
    float a = PI / s - atan(p.x, p.y);
    float n = PI2 / s;
    a = floor(a / n) * n;
    p *= rotate(a);
    return p;
}

vec2 bend(in vec2 p, in float d)
{
    p.y -= d*p.x*p.x;
    p.y *= cos(atan(2.0*d*p.x));
	return p;
}

vec3 opCheapBend( vec3 p )
{
    float c = cos(20.0*p.y);
    float s = sin(20.0*p.y);
    mat2  m = mat2(c,-s,s,c);
    vec3  q = vec3(m*p.xy,p.z);
	return q;
}

float sphereDist( vec3 p, vec3 c, float r ) {

	return length( p - c ) - r;

}

float udRoundBox( vec3 p, vec3 b, float r )
{
  return length(max(abs(p)-b,0.0))-r;
}

float udBox( vec3 p, vec3 b )
{
  return length(max(abs(p)-b,0.0));
}

float floorDist( vec3 p ){

	return dot(p, vec3( 0.0, 1.0, 0.0 ) ) + 0.0;

}

float sdCapsule( vec3 p, vec3 a, vec3 b, float r )
{
    vec3 pa = p - a, ba = b - a;
    float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
    return length( pa - ba*h ) - r;
}

float sdCylinder( vec3 p, float width )
{
	return length( p.xz ) - width;
}

float barDist(vec3 p, float width) {
	return length(max(abs(p.xz) - width, 0.0));
}

float sdCappedCylinder( vec3 p, vec2 h )
{
	vec2 d = abs(vec2(length(p.xz),p.y)) - h;
	return min(max(d.x,d.y),0.0) + length(max(d,0.0));
}

// utils
vec2 getDiscrete( vec2 p, float grid ) {
	return floor(p / grid);
}

// color functions
vec3 hsv2rgb( vec3 c ) {

	vec4 K = vec4( 1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0 );
	vec3 p = abs( fract( c.xxx + K.xyz ) * 6.0 - K.www );
	return c.z * mix( K.xxx, clamp( p - K.xxx, 0.0, 1.0 ), c.y );

}

// thanks to http://glslsandbox.com/e#21290.5
vec2 circuitPattern( vec2 p ) {

	p = fract(p);
	float r = 0.123;
	float v = 0.0, g = 0.0;
	r = fract(r * 9184.928);
	float cp, d;
	
	d = p.x;
	g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 1000.0);
	d = p.y;
	g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 1000.0);
	d = p.x - 1.0;
	g += pow(clamp(3.0 - abs(d), 0.0, 1.0), 1000.0);
	d = p.y - 1.0;
	g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 10000.0);
	
	const int iter = 15;
	for(int i = 0; i < iter; i ++)
	{
		cp = 0.5 + (r - 0.5) * 0.9;
		d = p.x - cp;
		g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 200.0);
		if(d > 0.0) {
			r = fract(r * 4829.013 * 2.0);
			p.x = (p.x - cp) / (1.0 - cp);
			v += 1.0;
		}
		else {
			r = fract(r * 1239.528);
			p.x = p.x / cp;
		}
		p = p.yx;
	}
	v /= float(iter);
	return vec2(g, v);

}

float checkeredPattern( vec2 p ) {

	float u = 1.0 - floor( mod( p.x, 2.0 ) );
	float v = 1.0 - floor( mod( p.y, 2.0 ) );

	if ( ( u == 1.0 && v < 1.0 ) || ( u < 1.0 && v == 1.0 ) ) {

		return 0.2;

	} else {

		return 1.0;

	}

}

Intersect minIntersect( Intersect a, Intersect b ) {

	if ( a.distance < b.distance ) {
		return a;
	} else {
		return b;
	}

}

Intersect maxIntersect( Intersect a, Intersect b ) {

	if ( a.distance > b.distance ) {
		return a;
	} else {
		return b;
	}

}

float rand(vec2 co){
	return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

float psylliumDistCapsule( vec3 p ) {

	float t = 8.0 * time;
	float rnd = rand( floor( p.xz / GRID ) );
	float x = rnd * 8.0 - 4.0;
	float cost = cos( t + 2.0 * rnd );
	vec3 org = vec3( x, -250.0 + cost + length( p.xz ) * 0.2, x );
	vec3 dir = vec3( cost, 2.0, -2.0 + cost );

	return sdCapsule( onRep( p, GRID ), org, org + 4.0 * normalize( dir ), 0.2 );
}

float psylliumDistCylinder( vec3 p ) {

	float t = 8.0 * time;
	float rnd = rand( floor( p.xz / GRID ) );
	float x = rnd * 6.0 - 3.0;
	float cos_t = cos( t );

	vec3 q = p;
	q.y += 200.0 - length( p.xz ) * 0.2;
	q = onRep( q, GRID );
	q.yz *= rotate( cos_t - 0.5 );
	q.y -= 3.0;
	q.x -= x;

	return sdCappedCylinder( q, vec2( 0.1, 3.0 ) );
}

float laserDist( vec3 p ) {
	vec3 q = p;
	q.xy *= rotate( -1.0 );

	return min(
		sdCapsule( onRepX( q, 40.0), vec3( 22.0 * sin( time * 2.0 ), -1000.0, 1000.0 ), vec3( 0.0, 10000.0, -10000.0), 0.2 ),
		sdCapsule( onRepX( p, 30.0) , vec3( 17.0 * cos( time * 2.0 ), -1000.0, 1000.0 ), vec3( 0.0, 10000.0, -10000.0), 0.2 ));
}

// float sceneDist( vec3 p ) {
// 	vec3 q = p;
// 	q.xz *= rotate( -2.0 );
// 
// 	vec3 p2 = p;
// 	p2.xz = fold( p2.xz, 8.0 );
// 	p2.z -= 20.0;
// 	p2.yz *= rotate( -0.5 );
// 
// 	return 
// 		min(
// 			min(
// 				max(
// 					sphereDist( p, vec3( 0.0, 0.0, 0.0 ), 15.0 ),
// 					udRoundBox( p + vec3( 0.0, 20.0, 0.0 ), vec3( 20.0, 20.0, 20.0 ), 0.1 )
// 				),
// 				max(
// 					sphereDist( p, vec3( 0.0, 10.0, 0.0 ), 24.0 ),
// 					udRoundBox( p2, vec3( 1.0, 5.0, 10.0 ), 0.1 )
// 				)
// 			),
// 			sphereDist( onRep( q, 500.0), vec3( -150.0, 100.0, -150.0 ), 100.0 )
// 		);
// }

// Intersect sceneIntersect( vec3 p ) {
// 
// 	Intersect a, b, c, d;
// 
// 	vec3 q = p;
// 	q.xz *= rotate( -2.0 );
// 
// 	vec3 p2 = p;
// 	p2.xz = fold( p2.xz, 8.0 );
// 	p2.z -= 20.0;
// 	p2.yz *= rotate( -0.5 );
// 
// 	a.distance = sphereDist( p, vec3( 0.0, 0.0, 0.0 ), 15.0 );
// 	a.material = MIRROR_MATERIAL;
// 
// 	b.distance = udRoundBox( p + vec3( 0.0, 20.0, 0.0 ), vec3( 20.0, 20.0, 20.0 ), 0.1 );
// 	b.material = FLOOR_MATERIAL;
// 
// 	c.distance = sphereDist( onRep( q, 500.0), vec3( -150.0, 100.0, -150.0 ), 100.0 );
// 	c.material = CIRCUIT_MATERIAL;
// 
// 	d.distance = max(
// 					sphereDist( p, vec3( 0.0, 10.0, 0.0 ), 24.0 ),
// 					udRoundBox( p2, vec3( 1.0, 5.0, 10.0 ), 0.1 )
// 				);
// 	d.material = FLOOR_MATERIAL2;
// 	d.position = p2;
// 
// 	return minIntersect( minIntersect( maxIntersect( a, b ), d ), c);
// }

float sceneDist( vec3 p ) {
	vec3 q = p;
	q.xz *= rotate( -2.0 );

	return 
		min(
			max(
				sphereDist( p, vec3( 0.0, 0.0, 0.0 ), 12.0 ),
				udRoundBox( p + vec3( 0.0, 20.0, 0.0 ), vec3( 20.0, 20.0, 20.0 ), 0.1 )
			),
			sphereDist( onRep( q, 500.0), vec3( -150.0, 100.0, -150.0 ), 100.0 )
		);
}

Intersect sceneIntersect( vec3 p ) {

	Intersect a, b, c, d;

	vec3 q = p;
	q.xz *= rotate( -2.0 );

	a.distance = sphereDist( p, vec3( 0.0, 0.0, 0.0 ), 12.0 );
	a.material = MIRROR_MATERIAL;

	b.distance = udRoundBox( p + vec3( 0.0, 20.0, 0.0 ), vec3( 20.0, 20.0, 20.0 ), 0.1 );
	b.material = FLOOR_MATERIAL;

	c.distance = sphereDist( onRep( q, 500.0), vec3( -150.0, 100.0, -150.0 ), 100.0 );
	c.material = CIRCUIT_MATERIAL;

	return minIntersect( maxIntersect( a, b ), c );
}

//vec3 getNormal( vec3 p ) {
//
//	return normalize(vec3(
//		sceneDist(p + vec3( EPS, 0.0, 0.0 ) ) - sceneDist(p + vec3( -EPS, 0.0, 0.0 ) ),
//		sceneDist(p + vec3( 0.0, EPS, 0.0 ) ) - sceneDist(p + vec3( 0.0, -EPS, 0.0 ) ),
//		sceneDist(p + vec3( 0.0, 0.0, EPS ) ) - sceneDist(p + vec3( 0.0, 0.0, -EPS ) )
//	));
//
//}

vec3 getNormal( in vec3 pos ) {
	vec2 e = vec2( 1.0, -1.0 ) * 0.001;
	return normalize(
		e.xyy * sceneDist( pos + e.xyy ) + e.yyx * sceneDist( pos + e.yyx ) + 
		e.yxy * sceneDist( pos + e.yxy ) + e.xxx * sceneDist( pos + e.xxx ) );
}

Intersect getRayColor( vec3 origin, vec3 ray ) {

	// marching loop
	float dist, minDist;
	float depth = 0.0;
	vec3 p = origin;
	Intersect nearest;

	for ( int i = 0; i < 30; i++ ){
		dist = sceneDist( p );
		depth += dist;
		p = origin + depth * ray;

		if ( abs(dist) < EPS ) break;
	}

	vec3 psylliumColor = hsv2rgb( vec3( time * 0.02, 1.0, 1.0 ) );

	if ( abs(dist) < EPS ) {
		nearest = sceneIntersect( p );

		if ( nearest.material == CIRCUIT_MATERIAL ) {
			vec2 uv = p.yz / 1000.0;
			uv.xy += time * 0.01;
			vec2 dg = circuitPattern(uv);
			float pattern = max( dg.x - 1.0, 0.0 );
			nearest.color = psylliumColor * pattern;
		} else if ( nearest.material == FLOOR_MATERIAL ) {
			nearest.color = vec3( 0.9 ) * checkeredPattern( p.xz );
		//} else if ( nearest.material == MIRROR_MATERIAL ) {
		} else {
			nearest.normal = getNormal(p);
			nearest.color = vec3( 0.0 );
		}
		nearest.isHit = true;
	} else {
		nearest.color = vec3( 0.0 );
		nearest.isHit = false;
	}

	nearest.color *= clamp( 500.0 / depth, 0.0, 1.0 );
	nearest.position = p;
	nearest.distance = depth;

	// psyllium pass
	p = origin;
	depth = 0.0;
	minDist = 9999.9;
	vec3 minPos;
	for ( int i = 0; i < 15; i++ ){
		dist = psylliumDistCapsule( p );
		if ( dist < minDist ) {
			minDist = dist;
			minPos = p;
		}
		depth += dist;
		p = origin + depth * ray;

		if ( depth > nearest.distance ) break;
	}


	if ( !nearest.isHit || depth < nearest.distance ) {
		float l = length( minPos - origin );
		float k = clamp( 400.0 / l, 0.0, 1.0 );
		if ( abs(dist) < ROUGH_EPS ) {
			nearest.color += k * psylliumColor;
		} else {
			nearest.color += k * psylliumColor * clamp( 0.3 / minDist, 0.0, 1.0 );
		}
	}

	// laser pass
	p = origin;
	depth = 0.0;
	minDist = 9999.9;
	for ( int i = 0; i < 20; i++ ){
		dist = laserDist( p );
		minDist = min(dist, minDist);
		depth += dist;
		p = origin + depth * ray;
	}

	psylliumColor = vec3( 1.0 ) - psylliumColor;

	if ( abs(dist) < EPS ) {
		nearest.color += psylliumColor;
	} else {
		nearest.color += psylliumColor * clamp( 0.3 / minDist, 0.0, 1.0 );
	}

	return nearest;

}

void main(void) {

	// fragment position
	vec2 p = ( gl_FragCoord.xy * 2.0 - resolution ) / resolution;

	// camera and ray
	cPos = cameraPosition;
	vec4 rayTip = cameraWorldMatrix * cameraProjectionMatrixInverse * vec4( p.xy, 1.0, 1.0 );
	vec3 ray = normalize( rayTip.xyz );

	Intersect nearest = getRayColor( cPos, ray );
	Intersect firstNearest = nearest;
	vec3 color = nearest.color;

	if ( nearest.material == MIRROR_MATERIAL && nearest.isHit ) {
		cPos = nearest.position + nearest.normal * OFFSET;
		ray = normalize( reflect( ray, nearest.normal ) );
		nearest = getRayColor( cPos, ray );

		color += 0.8 * nearest.color;
	}

	if ( firstNearest.isHit ) {

		float z = -( viewMatrix * vec4( firstNearest.position, 1.0 ) ).z;
		float normalZ = ( z - cameraNear ) / ( cameraFar - cameraNear );
		gl_FragDepthEXT = cameraFar * normalZ / z;

	} else {

		gl_FragDepthEXT = 1.0;

	}

	gl_FragColor = vec4( color, 1.0 );

}

		</script>

		<script id="vertex_shader" type="x-shader/x-vertex">

			attribute vec3 position;

			void main(void) {

				gl_Position = vec4(position, 1.0);

			}

		</script>

		<script src="js/three.min.js"></script>
		<script src="js/controls/OrbitControls.js"></script>

		<script src="js/libs/charsetencoder.min.js"></script>
		<script src="js/libs/ammo.js"></script>
		<script src="js/loaders/TGALoader.js"></script>
		<script src="js/loaders/MMDLoader.js"></script>
		<script src="js/animation/CCDIKSolver.js"></script>
		<script src="js/animation/MMDPhysics.js"></script>

		<script src="js/Detector.js"></script>
		<script src="js/libs/stats.min.js"></script>
		<script src="js/libs/dat.gui.min.js"></script>

		<script>

			var container, stats;
			var camera, dummyCamera, scene, flyControls, orbitControls, renderer;

			var geometry, material, plane;
			var mouse = new THREE.Vector2( 0.5, 0.5 );
			var mesh, camera, scene, renderer;
			var helper;

			var ready = false;

			var clock = new THREE.Clock();

			var config = {
				saveImage: function() {
					renderer.render( scene, camera );
					if ( mesh ) {
						helper.render( scene, camera );
					}

					window.open( canvas.toDataURL() );
				},
				camera: 'Auto',
				resolution: '512',
			};

			init();
			animate();

			function init() {

				container = document.createElement( 'div' );
				document.body.appendChild( container );

				camera = new THREE.PerspectiveCamera( 45, window.innerWidth / window.innerHeight, 1, 2000 );
				camera.position.y = 30;
				camera.position.z = 20;

				// scene

				scene = new THREE.Scene();

				var ambient = new THREE.AmbientLight( 0x666666 );
				scene.add( ambient );

				var directionalLight = new THREE.DirectionalLight( 0x887766 );
				directionalLight.position.set( -1, 1, 1 ).normalize();
				scene.add( directionalLight );

				//

				renderer = new THREE.WebGLRenderer();
				//renderer.setPixelRatio( window.devicePixelRatio );
				renderer.setSize( config.resolution, config.resolution );
				renderer.setClearColor( new THREE.Color( 0xffffff ) );
				canvas = renderer.domElement;
				container.appendChild( canvas );

				// check gl_FragDepthEXT
				var gl = canvas.getContext( 'webgl' ) || canvas.getContext( 'experimental-webgl' );
				var ext = gl.getExtension( 'EXT_frag_depth' );
				if( ext == null ) {
					alert( "gl_FragDepthEXT not supported" );
					return;
				}

				orbitControls = new THREE.OrbitControls( camera, canvas );
				orbitControls.enablePan = true;
				orbitControls.keyPanSpeed = 0.01;
				orbitControls.enableDamping = false;
				orbitControls.dampingFactor = 0.015;
				orbitControls.enableZoom = true;
				orbitControls.zoomSpeed = 1;
				orbitControls.rotateSpeed = 0.8;
				orbitControls.autoRotate = false;
				orbitControls.autoRotateSpeed = 0.0;
				orbitControls.target = new THREE.Vector3( 0.0, 10.0, 0.0 );

				// raymarching plane
				geometry = new THREE.PlaneBufferGeometry( 2.0, 2.0 );
				material = new THREE.RawShaderMaterial( {
					uniforms: {
						resolution: { type: 'v2', value: new THREE.Vector2( config.resolution, config.resolution ) },
						mouse: { type: 'v2', value: mouse },
						time: { type: 'f', value: 0.0 },

						cameraWorldMatrix: { type: 'm4', value: camera.matrixWorld },
						cameraProjectionMatrixInverse: { type: 'm4', value: new THREE.Matrix4().getInverse( camera.projectionMatrix ) },
						cameraNear: { type: 'f', value: camera.near },
						cameraFar:  { type: 'f', value: camera.far },
					},
					vertexShader: document.getElementById( 'vertex_shader' ).textContent,
					fragmentShader: document.getElementById( 'fragment_shader' ).textContent
				} );
				plane = new THREE.Mesh( geometry, material );
				plane.frustumCulled = false;
				plane.position.z = -1;
				scene.add( plane );

				// model

				var onProgress = function ( xhr ) {
					if ( xhr.lengthComputable ) {
						var percentComplete = xhr.loaded / xhr.total * 100;
						console.log( Math.round(percentComplete, 2) + '% downloaded' );
					}
				};

				var onError = function ( xhr ) {
				};

				var modelFile = 'models/mmd/miku/miku_v2.pmd';
				var vmdFiles = [ 'models/mmd/vmd/wavefile_v2.vmd' ];
				var cameraFiles = [ 'models/mmd/vmd/wavefile_camera.vmd' ];
				var audioFile = 'models/mmd/audio/wavefile_short.mp3';
				var audioParams = { delayTime: 160 * 1 / 30 };

				helper = new THREE.MMDHelper( renderer );

				var loader = new THREE.MMDLoader();
				loader.setDefaultTexturePath( './models/mmd/default/' );

				loader.load( modelFile, vmdFiles, function ( object ) {

					mesh = object;

					helper.add( mesh );
					helper.setAnimation( mesh );
					helper.doOutlineDrawing = false;
					/*
					 * Note: You must set Physics
					 *       before you add mesh to scene or any other 3D object.
					 * Note: Physics calculation is pretty heavy.
					 *       It may not be acceptable for most mobile devices yet.
			 		 */
					if ( ! isMobileDevice() ) {

						helper.setPhysics( mesh );

					}

					loader.loadVmds( cameraFiles, function ( vmd ) {

						helper.setCamera( camera );

						loader.pourVmdIntoCamera( camera, vmd );
						helper.setCameraAnimation( camera );

						loader.loadAudio( audioFile, function ( audio, listener ) {

							listener.position.z = 1;

							helper.setAudio( audio, listener, audioParams );

							/*
							 * Note: call this method after you set all animations
							 *       including camera and audio.
							 */
							helper.unifyAnimationDuration();

							scene.add( audio );
							scene.add( listener );
							scene.add( mesh );

							ready = true;

						}, onProgress, onError );


					}, onProgress, onError );


				}, onProgress, onError );

				//
				window.addEventListener( 'resize', onWindowResize, false );

				var gui = new dat.GUI();
				gui.add( config, 'saveImage' ).name( 'Save Image' );
				gui.add( config, 'camera', [ 'Auto', 'Orbit' ] ).name( 'Camera' ).onChange( function( value ) {

					helper.doCameraAnimation = ( value == 'Auto' );

				} );

				gui.add( config, 'resolution', [ '256', '512', '800', 'full' ] ).name( 'Resolution' ).onChange( function( value ) {

					if ( value !== 'full' ) {

						canvas.width = value;
						canvas.height = value;

					}
					onWindowResize();

				} );

				stats = new Stats();
				document.body.appendChild( stats.domElement );

				onWindowResize();

			}

			function onWindowResize() {

				if ( config.resolution === 'full' ) {

					canvas.width = window.innerWidth;
					canvas.height = window.innerHeight;

				}

				camera.aspect = canvas.width / canvas.height;
				camera.updateProjectionMatrix();

				renderer.setSize( canvas.width, canvas.height );

			}

			function animate( timestamp ) {

				requestAnimationFrame( animate );
				render( timestamp );

			}

			function render( timestamp ) {

				var delta = clock.getDelta();

				stats.begin();

				if ( config.camera === "Fly" ) {

					flyControls.update( delta );

				} else if ( config.camera === "Orbit" ) {

					orbitControls.update();

				}

				if ( config.objAutoMove) {

					config.objY = Math.abs( Math.sin( timestamp * 2.0 * 0.001 ) * 30.0 );

				}

				material.uniforms.resolution.value = new THREE.Vector2( canvas.width, canvas.height );
				material.uniforms.mouse.value = mouse;
				material.uniforms.time.value = timestamp * 0.001;

				material.uniforms.cameraWorldMatrix.value = camera.matrixWorld;
				material.uniforms.cameraProjectionMatrixInverse.value = new THREE.Matrix4().getInverse( camera.projectionMatrix );
				material.uniforms.cameraNear.value = camera.near;
				material.uniforms.cameraFar.value  = camera.far;

				renderer.render( scene, camera );

				stats.end();

				if ( ready ) {

					helper.animate( delta );
					helper.render( scene, camera );

				} else {

					renderer.clear();
					renderer.render( scene, camera );

				}

			}

			// easy mobile device detection
			function isMobileDevice () {

				if ( navigator === undefined || navigator.userAgent === undefined ) {

					return true;

				}

				var s = navigator.userAgent;

				if (    s.match( /iPhone/i )
//					 || s.match( /iPad/i )
					 || s.match( /iPod/i )
					 || s.match( /webOS/i )
					 || s.match( /BlackBerry/i )
					 || ( s.match( /Windows/i ) && s.match( /Phone/i ) )
					 || ( s.match( /Android/i ) && s.match( /Mobile/i ) ) ) {

					return true;

				}

				return false;

			}

		</script>

	</body>
</html>
