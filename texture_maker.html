<!DOCTYPE html>
<!--
Copyright (c) 2015 gam0022
Released under the MIT license
http://opensource.org/licenses/mit-license.php
-->
<html>
  <head>
    <meta charset="utf-8">
    <title>Steel Frame</title>
  </head>
  <body>
    <script id="fragment_shader" type="x-shader/x-fragment">
    precision highp float;
    uniform float time;
    uniform vec2 mouse;
    uniform vec2 resolution;

    const float EPS = 0.001;

    // distance functions
    vec2 onRep(vec2 p, float interval) {
      return mod(p, interval) - interval * 0.5;
    }

    float barDist(vec2 p, float interval, float width) {
      return length(max(abs(onRep(p, interval)) - width, 0.0));
    }

    float tubeDist(vec2 p, float interval, float width) {
      return length(onRep(p, interval)) - width;
    }

    float sceneDist(vec3 p) {
      float bar_x = barDist(p.yz, 1.0, 0.1);
      float bar_y = barDist(p.xz, 1.0, 0.1);
      float bar_z = barDist(p.xy, 1.0, 0.1);

      float tube_x = tubeDist(p.yz, 0.1, 0.025);
      float tube_y = tubeDist(p.xz, 0.1, 0.025);
      float tube_z = tubeDist(p.xy, 0.1, 0.025);

      return max(max(max(min(min(bar_x, bar_y),bar_z), -tube_x), -tube_y), -tube_z);
    }

    vec3 getNormal(vec3 p) {
      return normalize(vec3(
        sceneDist(p + vec3(  EPS, 0.0, 0.0)) - sceneDist(p + vec3( -EPS, 0.0, 0.0)),
        sceneDist(p + vec3(0.0,   EPS, 0.0)) - sceneDist(p + vec3(0.0,  -EPS, 0.0)),
        sceneDist(p + vec3(0.0, 0.0,   EPS)) - sceneDist(p + vec3(0.0, 0.0,  -EPS))
      ));
    }

    void main_(void) {
      // fragment position
      vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);

      // camera and ray
      vec3 cPos = vec3(mouse.x - 0.5, mouse.y - 0.5, time);
      vec3 cUp  = normalize(vec3(0.1, 0.4, 0.0));
      vec3 cDir = cross(cUp, vec3(-1.0, 0.0, 0.0));
      vec3 cSide = cross(cDir, cUp);
      float targetDepth = 1.0;
      vec3 ray = normalize(cSide * p.x + cUp * p.y + cDir * targetDepth);

      // direction light
      vec3 lightDir = normalize(vec3(1, 1, -2));

      // marching loop
      float dist;
      float depth = 0.0;
      vec3 dPos = cPos;
      for(int i = 0; i < 64; i++){
        dist = sceneDist(dPos);
        depth += dist;
        dPos = cPos + depth * ray;
        if (abs(dist) < EPS) break;
      }

      // hit check
      vec3 color;
      if (abs(dist) < EPS) {
        vec3 normal = getNormal(dPos);
        float diffuse = clamp(dot(lightDir, normal), 0.1, 1.0);
        color = vec3(1.0, 0.1, 0.1) * diffuse;
      } else {
        color = vec3(0.0);
      }
      gl_FragColor = vec4(color + 0.05 * depth, 1.0);
    }

    void main(void) {
      vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);
      float s = 18.0;
      float c = 2.0;
      p *= s;
      p = mod(p, c) - 0.5 * c;
      vec3 color = vec3(0.5);
      if (abs(p.x) + abs(p.y) < 1.0) {
        color = vec3(0.8);
      }
      gl_FragColor = vec4(color, 0.5);
    }
    </script>

    <script id="vertex_shader" type="x-shader/x-vertex">
    void main(void) {
      gl_Position = vec4(position, 1.0);
    }
    </script>

    <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r73/three.min.js" type="text/javascript"></script>
	<script type="text/javascript">
		var scene, camera, renderer;
		var geometry, material, mesh;
		var mouse = new THREE.Vector2(0.5, 0.5);
		var canvas;

		init();
		render();

		function init() {
		  scene = new THREE.Scene();
		  camera = new THREE.Camera();

		  geometry = new THREE.PlaneBufferGeometry(2.0, 2.0);
		  material = new THREE.ShaderMaterial({
		    uniforms: {
		      time: { type: "f", value: 0.0 },
		      resolution: { type: "v2", value: new THREE.Vector2(512.0, 512.0) },
		      mouse: { type: "v2", value: mouse }
		    },
		    vertexShader: document.getElementById('vertex_shader').textContent,
		    fragmentShader: document.getElementById('fragment_shader').textContent
		  });
		  mesh = new THREE.Mesh(geometry, material);
		  scene.add(mesh);

		  renderer = new THREE.WebGLRenderer();
		  renderer.setSize(512.0, 512.0);

		  canvas = renderer.domElement;
		  canvas.addEventListener('mousemove', onMouseMove);
		  document.body.appendChild(canvas);
		}

		function render(timestamp) {
		  requestAnimationFrame(render);
		  material.uniforms.time.value = timestamp * 0.001;
		  material.uniforms.mouse.value = mouse;
		  renderer.render(scene, camera);
		}

		function saveImage() {
      console.log("saveImage");
		  renderer.render(scene, camera);
		  window.open(canvas.toDataURL());
		}

		function onMouseMove(e) {
			mouse.x = e.offsetX / canvas.width;
			mouse.y = e.offsetY / canvas.height;
		}
	</script>
    <div><input type="button" value="save image" onclick="saveImage();"></div>
  </body>
</html>
