<script id="fs" type="x-shader/x-fragment">
// http://jsdo.it/gaziya/wXzq
#ifdef GL_ES
precision mediump float;
#endif

uniform float time;
uniform vec2 resolution;
    
#define PI	3.14159265359
#define PI2 PI*2.0
#define PIH PI*0.5
#define EPS 0.01
#define BB_OFFSET 0.1

#define HEAD -2.0
#define TAIL 2.0

/*
mat2 rotate(in float a) について
mat3を使ってvec3を回転するの時は、X軸用、y軸用,z軸用の行列を書かなければならない。
それを乗算してオイラー角用行列にするとか、わずらわしいと言うか、難しい事をしなくてはならない。
mat2を使ってxy平面を回す方が、簡単だしコストも安い。
shadertoyでも1年前くらいから使ってるskillだ。

p.xy *= rotate(0.3);　//z軸回し
p.yz *= rotate(0.3); //y軸回し
と続けざまに使えば、オイラー角まわしが出来る。
あくまで、原点での回転なので、前後に移動させる命令を書き忘れないように。
例えば
p.x -= 0.3;  // -=を使っているのは、座標情報が直観的に分かりやすくするためです。
p.xy *= rotate(iGlobalTime);
return length(max(abs(p)-vec3(0.1),0.0));  //boxが、座標vec3(0.3,0.0,0.0)においてz軸中心にクルクル回るDE(distance function)
上の2行の順番を変えると、3D迷子になるので注意。順番が大事です。
shaderToyでは、distance functionの事をDEと書いている人が多いので、それに倣ってます。
Distance Estimatedの略語ではないかと思われますが、自信ありません。
以下、distance functionの事をDEと呼ばせてもらいます。
一応、書いておきますが、引数 a は角度(radian)です。
(2016/2/23)
*/
mat2 rotate(in float a)
{
    float s=sin(a),c=cos(a);
    return mat2(c,s,-s,c);
}

/*
mat3 lookat(in vec3 eye, in vec3 target) について
まあ、名前の通り rayのベクトルを回す行列です。
引数は、eye(視点の座標) と target(目標の座標)
よく見るlookatは、up(うまく言えなけど”上方”って奴）の引数があるけど、
このfunctionはupをvec3(0.0,1.0,0.0)に決め打ちしました。
なので、真上から真下を見るとジンバルロックを起こします。
使ってはダメです。
せっかくなので何故ダメなのかの説明。
vec3 u = normalize(cross(w,vec3(0.0,1.0,0.0)));
この部分の w にvec3(0.0,-1.0,0.0)を入れたことを想像してください。(計算してはダメです）
crossは無限に現れます。
<追記>
ちょっと説明不足みたいなので補足します。
３Dのcrossは、２つのベクトルに対して直交するベクトルを出力します。
なので、２つのベクトルが一致しなければ、一つのベクトルが得られます。
コンパスで例えると、足の方向が２つのベクトルで、要のピンの軸方向がcrossの向きです。
２つのベクトルが一致（真逆のベクトルも含む）ということは、３Dにおいて1つのベクトル対して直交と同じなので、
無限に直交ベクトルが存在すると言う事です。
</追記>

このmat3は、 *= を使うと3D迷子になります。
rd = lookat(ro,ta)*rd;
みたいに使ってください。
行列が先でベクトルが後です。理由は、わかりません。経験則です。
(2016/2/23)
*/
mat3 lookat(in vec3 eye, in vec3 target)
{
	vec3 w = normalize(target-eye);
	vec3 u = normalize(cross(w,vec3(0.0,1.0,0.0)));
	vec3 v = normalize(cross(u,w));
    return mat3(u,v,w);
}

/*
クォータニオンについて
クォータニオンは、３Dにおいて任意軸で座標を回すSkillです。
クォータニオンは理解しないでも使えるので、気楽に使おう。
ステップは、２つ。
回転したい軸と回転角度(radian)を使ってクォータニオンを作る。
vec4 quat = quaternion(normalize(vec3(0.5,1.0,0.0)),0.7);
こんな感じ。
作ったクォータニオンと移動したい座標をqtransformに入れるを、移動後の座標が手に入る。
p = qtransform(quat,vec3(0.0,0.0,0.3));

qtransformは、ここ
http://d.hatena.ne.jp/edvakf/20111026/1319656727
より拝借しました。

invqは逆クォータニオンと言うのでしょうか？軸が同じで逆に回転させたモノ。
(2016/2/23)
*/
vec4 quaternion(in vec3 p, in float a)
{
	return vec4(p*sin(a/2.0),cos(a/2.0));
}

vec4 invq(in vec4 q)
{
    return vec4(-q.xyz,q.w);
}

vec3 qtransform(in vec4 q, in vec3 p)
{
	return p+2.0*cross(cross(p,q.xyz)-q.w*p,q.xyz);
}

//  ここまでクォータニオン関係
    
/*
float smin(in float a, in float b, in float k) について
これは、min関数をなめらかにするsmooth minimum　functionって事ですかね。
http://iquilezles.org/www/articles/smin/smin.htm
ここを見てください。
こんな使い方です。
ページの中段くらいに３つの sminが並んでますが、今回使ったのは、真ん中の奴です。
個人的には左の方が好きなのですが、今回は厳しい開発だったので直感でコストの安そうな真ん中にしました。
最近は
https://www.shadertoy.com/view/XtjXWD
に
float smin(float a, float b, float k)
{
	return log2(exp2(k*a)+exp2(k*b))/k;
}
ってのが登場しましたが、まだ試してません。
(2016/2/23)
*/
    
float smin(in float a, in float b, in float k)
{
    float h = clamp(0.5+0.5*(b-a)/k,0.0,1.0);
    return mix(b,a,h) - k*h*(1.0-h);
}    

/*
Foldingについて
文字通り折りたたみSkill.
放射線状に分割できるモノ。
コストパフォーマンスも良いし、いろんなモノに応用できます。
ちょっと説明が難しので,また後日頭を整理してから書きます。
とりあえず参考shaderを載せときます。
https://www.shadertoy.com/view/4tX3DS
https://www.shadertoy.com/view/llSSWt
https://www.shadertoy.com/view/Mlf3Wj
(2016/2/23)
*/
vec2 fold1(in vec2 p, in float a)
{
    p.x = abs(p.x);
    vec2 n = vec2(cos(a),sin(a));
    p -= 2.0*min(0.0,dot(p,n))*n;
    return p;    
}

vec2 fold3(in vec2 p, in float a)
{
    p.x = abs(p.x);
    vec2 n = vec2(cos(a),sin(a));
    p -= 2.0*min(0.0,dot(p,n))*n;
    n = normalize(vec2(n.x-1.0,n.y));
    p -= 2.0*min(0.0,dot(p,n))*n;
    n = normalize(vec2(n.x-1.0,n.y));
    p -= 2.0*min(0.0,dot(p,n))*n;
    return p;    
}
  
//  ここまで Folding    

/*
vec2 bend(in vec2 p, in float d) について
やっている事は、ある軸を2次関数を使って湾曲させ補正をかけてるだけです。
2次関数の微分を使っての補正は、 cos(atan(δ))　の一行。
これとsegmentとrotateを組み合わせてベジェ曲線の代用をしています。    
(2016/2/24)
*/    
vec2 bend(in vec2 p, in float d)
{
    p.y -= d*p.x*p.x;
    p.y *= cos(atan(2.0*d*p.x));
 	return p;   
}
    
/*
float gaussian(in float t) について
偏差値で使われる釣鐘状の曲線
https://ja.wikipedia.org/wiki/%E3%82%AC%E3%82%A6%E3%82%B9%E9%96%A2%E6%95%B0
これをbump用に使用する。
結構、使い勝手はいい。
(2016/2/23)
*/
float gaussian(in float t)
{
    return exp(-t*t);
}
    
/*
float lengthN(in vec2 p, in float n) について
柔らかな四角みたいな平面空間のDE。円からの変形です。
ここ
http://www.demoscene.jp/?p=1147
より拝借しました。説明は、このページに丸投げ。

今回のオブジェクトは3Dなのに、2Dを使うのが味噌になってます。
(2016/2/23)
*/
float lengthN(in vec2 p, in float n)
{
	p = pow(abs(p),vec2(n));
	return pow(p.x+p.y,1.0/n);
}

/*
vec2 segment(in vec2 p, in float a, in float b) について
これは軸上にある線の平面空間のDEなのだが、返り値が、vec2になってます。
ここで説明の為、返り値を p と置きます。
p.x は、線のDE。
p.y は、パラメトリック変数みたいな数。a の位置が 0.0 で b の位置が 1.0になってる。
これじゃ、説明として厳しいね。
vec2 segment(in vec2 p, in vec2 a, in vec2 b)
{
	vec2 pa=p-a,ba=b-a;
	float t=clamp(dot(pa,ba)/dot(ba,ba),0.0,1.0);
	return vec2(length(pa-ba*t), t);
}

vec2 segment(in vec2 p, in float a, in float b)
{
    return segment(p, vec2(a,0.0), vec2(b,0.0));
}

これと同等のfunctionです。
上の方は平面に線を書くfucntion
下の方は軸線上に線を書くfunction
x,y,z軸上限定で線を引くモノです。
基本、空間の湾曲、移動と回転しか使いません。
なので、x,y,z軸上限定の方がコントロールしやすいからです。

ちなみに、このshaderは、上記２つの２Ｄ用DEと３D用の球のDEしか使っていません。
後は空間の変形処理みたいな事だけです。
(2016/2/24)

*/    
vec2 segment(in vec2 p, in float a, in float b)
{
    float x=clamp(p.x,min(a,b),max(a,b));
    return vec2(length(p-vec2(x,0.0)),(x-a)/(b-a));
}

/*
float pattern(in vec2 p) について
これは、bodyのbumpに使ってる2Dの連続パターンです。
正6角形ではありませんが、その必要もないのでOKです。
(2016/2/24)
*/
float pattern(in vec2 p) {
    p *= vec2(8.0,25.0);
 	p.x = mod(p.x,2.0)-1.0;
    p.y = mod(p.y,4.0)-2.0;    
    float de = abs(abs(p.x)+clamp(abs(p.y)-1.0,-0.5,0.5)-0.5);
    return clamp(de+0.7,0.0,1.0);
}

vec2 bodyCurve(in float x)
{
    float t = time * 1.5;
    return vec2(
        0.05*cos(x*5.0+t)+0.1*cos(x*3.0+t),
        0.1*sin(x*5.0+t)+0.15*sin(x*3.0+t));
}
    
float barbCurve(in float x)
{
    float t = time*1.5;
 	return mix(0.0,0.02*sin(-40.0*x+t),clamp(x*5.0,0.0,1.0));   
}

float HairCurve(in float x)
{
    return mix(0.0,0.02*sin(35.0*x),clamp(x*6.0,0.0,1.0))+x*0.6;   
}

float mapTop(in vec3 p) // head parts
{
    float bump = 0.005*pattern(vec2(atan(p.y,p.z),p.x)*vec2(1.0,1.5));
    p -= vec3(-0.03,0.07,0.0);
    p *= vec3(0.8,1.5,1.0);
    float de = length(vec2(lengthN(p.xy,3.0),p.z))-0.07;
    p -= vec3(-0.047,-0.01,0.0);
    p.z = abs(p.z); p.z -= 0.035;
    de = smin(de, length(p)-0.035,0.01);    
    p.y -= 0.004;
    p.yz *= rotate(-0.1);
    p.y *= 1.8;
    de -= -0.06*gaussian(length(p.yz)*90.0)*step(0.0,-p.x);      
    de -= bump;
    return de;
}

float mapUpperJaw(in vec3 p) // head parts
{
    float bump = 0.005*pattern(vec2(atan(p.y,p.z),p.x)*vec2(1.0,1.5));    
    vec3 q = p;
    p.y -= 0.01;
    p.xy = bend(p.xy, 0.2);
    vec2 sg = segment(p.xy,-0.3,0.0);
    float de = lengthN(vec2(sg.x, p.z*0.6),2.5)-(0.02*sg.y*sg.y+0.03);
    p.x -= -0.3;
    p.z = abs(p.z); p.z -= 0.032;
    de -= 0.006*gaussian(length(p.xz)*70.0)*step(0.0,p.y);
    p.y -= 0.0045;
    p.yz *= rotate(-0.3);
    p.y *= 1.6;
    de -= -0.02*gaussian(length(p.yz)*120.0)*step(0.0,-p.x);   
    q.x -= -0.28;
    q.x *= 0.3;
    de -= 0.01*gaussian(length(q.xz)*70.0)*step(0.0,q.y);   
    de -= bump;
    return de;
}

float mapUpperFang(in vec3 p) // head parts
{
    p -= vec3(-0.3,-0.04,0.0);
    p.z = abs(p.z); p.z -= 0.04;
    vec2 sg = segment(p.yz,0.0,0.05);
    return lengthN(vec2(sg.x,p.x),1.5)-0.01*sg.y;
}

float mapBottomJaw(in vec3 p) // head parts
{
    float bump = 0.005*pattern(vec2(atan(p.y,p.z),p.x)*vec2(0.7,1.7))*step(-0.02,-p.y);
	p.xy = bend(p.xy,-0.2);
    vec2 sg = segment(p.xy,-0.25,0.0);
    float de = lengthN(vec2(sg.x, p.z*0.7),2.5)-(0.02*sg.y*sg.y+0.03);    
    de -= -0.008*gaussian(length(p.yz)*50.0)*step(0.0,-p.x);        
    p.x -= -0.1;
    p.x *= 0.15;   
    de -= -0.05*gaussian(length(p.xz)*60.0)*step(0.0,p.y);            
    de -= bump;
    return de;
}

float mapBottomFang(in vec3 p) // head parts
{
    p -= vec3(-0.24,0.017,0.0);
    p.z = abs(p.z); p.z -= 0.03;
    p.x = abs(p.x); p.x -= 0.01;
    p.x = abs(p.x); p.x -= 0.01;
    p.yz *= rotate(0.2);
    vec2 sg = segment(p.yz,0.03,0.0);
    return lengthN(vec2(sg.x,p.x),1.5)-0.01*sg.y;
}

float mapEyes(in vec3 p) // head parts
{
    p -= vec3(-0.115,0.065,0.0);
    p.z = abs(p.z); p.z -= 0.035;
    return length(p)-0.015;
}

float mapEars(in vec3 p) // head parts
{
    p -= vec3(-0.025,0.06,0.0);
    p.z = abs(p.z); p.z -= 0.08;
    p.z *=1.5;
    p.yz *= rotate(0.5);
    p.xy *= rotate(-0.3);
    vec2 sg = segment(p.yx,0.08,0.0);
    float de = lengthN(vec2(sg.x,p.z),5.0)-(0.02*sg.y+0.01);
    p.y -= 0.02;
    p.y *=0.5;
    de -= -0.02*gaussian(length(p.yx)*70.0)*step(0.0,p.z);
    return de;
}

float mapHorns(in vec3 p) // head parts
{
    p -= vec3(0.0,0.05,0.0);
    p.z = abs(p.z); p.z -= 0.04;
    p.xz *= rotate(0.3);
    p.xy *= rotate(-0.5);
    p.yz = bend(p.yz, 0.3);
    vec2 sg = segment(p.yz,0.25,0.0);
    return lengthN(vec2(sg.x,p.x),1.5)-0.01;
}
    
float mapBarb(in vec3 p) // head parts
{
    p -= vec3(-0.27,0.04,0.0);
    p.z = abs(p.z); p.z -= 0.05;
    p.zy *= rotate(0.5);
    p.xz *= rotate(-0.8);
    p.y -= barbCurve(p.z);
    float g = (barbCurve(p.z+EPS)-barbCurve(p.z-EPS))/(2.0*EPS);
    p.x *= cos(atan(g));
    vec2 sg = segment(p.zx,0.25,0.0);
    return 0.7*(length(vec2(sg.x, p.y))-(0.005*sg.y+0.003));
}

float mapHair(in vec3 p) // head parts
{
    p -= vec3(-0.02,0.055,0.0);
    p.zy = fold3(p.zy,radians(140.0));
    p.y -= 0.06;    
    p.y -= HairCurve(p.x);
    float g = (HairCurve(p.z+EPS)-HairCurve(p.z-EPS))/(2.0*EPS);
    p.x *= cos(atan(g));
    vec2 sg = segment(p.xz,0.12,0.0);
    return 0.7*(length(vec2(sg.x, p.y))-(0.01*sg.y+0.004));
}

float mapHead(in vec3 p)
{
    p.x -= HEAD;
    p.zy -= bodyCurve(HEAD);
    
    /*
    raymarching進行を高速にするためにbounding boxを作りました。
    rayがbounding box内に進入してない時はboxのDEを返し、進入したら細かいパーツ群のDEを返します。
    map関数(オブジェクト数)が少ないときは邪魔ですが、多いときは威力を発揮します。
    また、幾つかのグループに分けると更に効率が上がります。
    bounding boxの状態をみたい時は、BB_OFFSET を 0.0001くらいにしてください。
    bounding boxの位置と大きさは、悲しいくらい手作業です。

    vec3 q = p;
    q -= vec3(-0.1,0.07,0.0); // boxの中心位置
    vec3 bb = vec3(0.24,0.21,0.2)+BB_OFFSET;  // boxの大きさの半分の数値
    if (any(greaterThan(abs(q),bb))) return length(max(abs(q)-bb,0.0))+BB_OFFSET;
    この部分が、bounding box です。    
    将来的に動くボーンを組むことがあったとしたら、bounding sphere ってのも有りだなって考えてます。
    (2016/2/24)    
    */
    // bounding box
    vec3 q = p;
    q -= vec3(-0.1,0.07,0.0);
    vec3 bb = vec3(0.24,0.21,0.2)+BB_OFFSET;
    if (any(greaterThan(abs(q),bb))) return length(max(abs(q)-bb,0.0))+BB_OFFSET;

    // BottomJaw postion
    q = p;
    q.y -= -0.01;    
    q.xy *= rotate(0.1*sin(time)+0.3);
   
    float de = 1.0;
    de =  min(de, mapTop(p));
	de = smin(de, mapUpperJaw(p),0.02);
    de = smin(de, mapEars(p),0.03);
	de =  min(de, mapHair(p));
	de =  min(de, mapEyes(p));
	de =  min(de, mapHorns(p));
    de =  min(de, mapUpperFang(p));
    de =  min(de, mapBarb(p));
    de =  min(de, mapBottomJaw(q));
    de =  min(de, mapBottomFang(q));
    return de;
}

float mapBody(in vec3 p)
{
    p.zy -= bodyCurve(p.x);
	vec2 g = (bodyCurve(p.x+EPS)-bodyCurve(p.x-EPS))/(2.0*EPS);
    p.zy *= cos(atan(g));
    
    // bounding box
    vec3 q = p;
    q -= vec3(0.0,0.03,0.0);
    vec3 bb = vec3(2.2,0.2,0.15)+BB_OFFSET;
    if (any(greaterThan(abs(q),bb))) return length(max(abs(q)-bb,0.0))+BB_OFFSET;
    
    // main
    vec3 pMain = p;
    vec2 sg = segment(p.xy,TAIL,HEAD);
    float t = -abs(sg.y-0.6)*1.2+0.75;
    t = 0.1*smoothstep(0.0,0.25,t*t);
    
    float deMain = 0.8*(lengthN(vec2(sg.x,p.z*0.8),2.5)-(t+0.02));
    float a = atan(p.z,-p.y);    
    deMain -= -0.005*gaussian((abs(a)-0.6)*20.0)*step(0.0,-p.y);
    if (abs(a)<0.6)
    {
        deMain -= -0.005*gaussian(sin(p.x*PI2*6.0)*3.0)*step(0.0,-p.y);
        deMain -= 0.001*length(sin(vec2(p.x*10.0,a)*20.0));
    } else {
        deMain -= 0.005*pattern(vec2(a,p.x));
    }

    // fin
    p -= vec3(-0.4,t+0.03,0.0);

    /*
    空間分割について
    よく使うskill   mod(p,1.0)-0.5; みたいな無限増殖ではなくて、
    限定分割です。
    p.x = abs(p.x);
    p.x -= 0.2;
    float de = length(p) - 0.1;
    とすると2個の球ができます。
    これをloopさせて数を増やすのですが、倍々ゲームみたいに増えてくれません。
    ちなみに、これは、x軸上にリニアに増えてくパターン。
    この方法は、mod とは違って1軸限定です。
    でも、ちょっとだけ細工をすると、loop数が減ります。
    float s = 0.05;
    for(int i=0;i<14;i++){
        p.x = abs(p.x);
        p.x -= s*2.0;
    }
    p.x += s;    
    最後の1行がポイント。最後の1行は、 += です。
    相変わらず理由は、わかりません。きっと紙と鉛筆で考えればわかりそうですが。
    これも偶然見つけました。
    
    ついでなので、modの方のskillも１つ書いておきます。
    Sparse grid 
    https://www.shadertoy.com/view/XlfGDs
    ここに登場しました。
    無限増殖からの乱数を使った間引きです。
    自分でも、これを使ったshaderがあります。
    https://www.shadertoy.com/view/MtS3Rz
    https://www.shadertoy.com/view/lt2SDd
    もしshadertoyでこのskillを使うなら
    Sparse grid marching　[nimitz]
    https://www.shadertoy.com/view/XlfGDs
    への参照を書いておいてください。
    礼儀もあるけど、元情報へのリンクは初見の人には。とても役に立ちますから。
    そうは、いってもスタンダードになったskillに、イチイチlink貼ってたらウザいのでバランスに注意してください。
    
    もう１つ思い出しました。
    abs(ｎ)を違う書き方をすると
    n-2.0*min(0.0,n)
    になります。
    これにsmin()を使うというのも面白いです。
    float smin(in float a, in float b, in float k)
    {
	    float h = clamp(0.5+0.5*(b-a)/k,0.0,1.0);
	    return mix(b,a,h)-k*h*(1.0-h);
    }
      
    float sabs(in float n, in float k)
    {
        return  n-2.0*smin(0.0,n,k);  
    }
    こんな感じです。
    https://www.shadertoy.com/view/ldG3zc
    で使ってます。
    これは利用価値は低そうですけどね。参考まで。
    (2016/2/25)
    */
    float s = 0.05;
    for(int i=0;i<14;i++){
        p.x = abs(p.x);
        p.x -= s*2.0;
    }
    p.x += s;
    
    p.y *= 0.4;
    p.xy *= rotate(0.8);
    float deFin = max(abs(p.z)-0.01,lengthN(p.xy,5.0)-0.02);
    
    float de = 1.0;
    de =  min(de,deMain);
    de = smin(de,deFin,0.01);
    return de;
}

float  mapArm1(in vec3 p) // arms parts
{
    vec2 sg = segment(p.zx,0.3,0.0);
    float de = lengthN(vec2(sg.x, p.y*1.2),2.5)-(0.05*sg.y+0.05);
    de -= 0.005*pattern(vec2(atan(p.y,p.x),p.z)*vec2(0.7,1.8));
    return de;
}

float  mapArm2(in vec3 p) // arms parts
{
    vec2 sg = segment(p.zx,0.3, 0.0);
    float de = lengthN(vec2(sg.x, p.y*1.2),2.5)-(0.02*sg.y+0.04);
    de -= 0.005*pattern(vec2(atan(p.y,p.x),p.z)*vec2(0.7,1.8));
    return de;
}
    
float  mapHand(in vec3 p) // arms parts
{
    p -= vec3(-0.05,-0.02,0.0);
 	p.zx = fold1(p.zx,radians(110.0));
    p.xy *= rotate(-0.5);
    vec2 sg = segment(p.xz,0.08,0.0);
    float deHand = lengthN(vec2(sg.x,p.y),3.0)-(0.02*sg.y+0.02);
    p.x -= 0.08;
    p.xy *= rotate(-0.6);
    p.xy = bend(p.xy, -5.0);
    sg = segment(p.xz,0.1,0.0);
    float deClaw = lengthN(vec2(sg.x,p.y),2.5)-(0.02*sg.y);
    return smin(deHand, deClaw, 0.03);
}

float  mapClaw(in vec3 p) // arms parts for coloring
{
    p -= vec3(-0.05,-0.02,0.0);
 	p.zx = fold1(p.zx,radians(110.0));
    p.xy *= rotate(-0.5);
    p.x -= 0.08;
    p.xy *= rotate(-0.6);
    p.xy = bend(p.xy, -5.0);
    vec2 sg = segment(p.xz,0.1,0.0);
    return lengthN(vec2(sg.x,p.y),2.5)-(0.02*sg.y);
}

float  mapBall(in vec3 p, in float s) // arms parts
{
    p -= vec3(-0.05,-0.12,0.01);
    if (s<0.0) return length(p)-0.06;
    return 1.0;
}    

float mapArms(in vec3 p)
{
    const float offset = -1.2;
    p.x -= offset;
    p.zy -= bodyCurve(offset);
    
    // bounding box
    vec3 q = p;
    q -= vec3(-0.015,-0.12,0.0);
    vec3 bb = vec3(0.28,0.25,0.62)+BB_OFFSET;
    if (any(greaterThan(abs(q),bb))) return length(max(abs(q)-bb,0.0))+BB_OFFSET;

	float s = sign(p.z);   
    p.z = abs(p.z);
    
    /*
    クォータニオンの操作について
    下の３つのブロックでボーン操作をやってます。
    今回のボーン操作は、標準のやり方と違うような気もしますが Fake Bone ってことで。
    今回は、p.z = abs(p.z);　を使ってるので左右対称ですが、左右は同じ形です。
    でも、timeを組み込んでいないので動きません。
    面倒だけど左右別々の動きも可能です。
    今回は、とりあえず体裁さえ整ってれば良いやで作っているので、プロセスをbrush upしてません。
    興味のある方は、使いやすいように作ってください。
    理解させる説明が、きつくなってきたので、操作の説明だけにさせてもらいます。
    
    ボーンは、両腕を水平に広げたモノが初期姿勢になってます。
    つまり、ボーンの基準位置？が、z軸上に並んでると思ってください。
    ボーンに限らず、今回使ってる全てのDEは、x,y,z軸のどれかの上の範囲からの距離を使ってます。
    // 腕全体(上腕)の基本位置に移動(pは上書き)
 	p -= vec3(0.0, 0.0, 0.15);
    // 上腕の回転の為のクォータニオンを作る。
    quat = quaternion(normalize(vec3(0.5,1.0,0.0)),-0.7);
    // 上腕のDEを取得する為の座標の変換
    // これを上書きするとコントロール出来ないので、別のvec3を用意してください。
    // この場合,p1。　p1をmap関数に入れてDEを取得。
    vec3 p1 = qtransform(quat, p);
    
    // 前腕の基本位置に移動(pは上書き)
    // 上腕の長さは、0.3に設定してあります。なので回すベクトルはvec3(0.0,0.0,0.3)です。
    // 上で作ったクォータニオンの逆クォータニオンを使って前腕の基本位置を出し移動。
    // 何故、逆クォータニオンなのか？　理由は、わかってません。試行錯誤したらそうなってました。
    // この部分は確証がないので、間違ってたら教えてください。
    // 以下、同じことの繰り返しです。
    // 通常のボーンは親を回転させると子も回転すると思いますが、この方法だと子は回転しません。
    // 位置の移動だけです。なので絶対位置で回転してください。
    // これで、うまく伝わればいいのですが、後はソースを読んでください。
    // 一応、成功してるみたいなので、実績あります。
    p -= qtransform(invq(quat),vec3(0.0,0.0,0.3));
    quat = quaternion(normalize(vec3(-0.7,1.0,0.0)),1.0);
    vec3 p2 = qtransform(quat, p);
    
    p -= qtransform(invq(quat),vec3(0.0,0.0,0.3));
    quat = quaternion(normalize(vec3(0.0,0.0,1.0)),0.5);
    vec3 p3 = qtransform(quat, p);
    
    (2016/2/25)
    */
    vec4 quat;
	p -= vec3(0.0, 0.0, 0.15);
    quat = quaternion(normalize(vec3(0.5,1.0,0.0)),-0.7);
    vec3 p1 = qtransform(quat, p);
    
    p -= qtransform(invq(quat),vec3(0.0,0.0,0.3));
    quat = quaternion(normalize(vec3(-0.7,1.0,0.0)),1.0);
    vec3 p2 = qtransform(quat, p);
    
    p -= qtransform(invq(quat),vec3(0.0,0.0,0.3));
    quat = quaternion(normalize(vec3(0.0,0.0,1.0)),0.5);
    vec3 p3 = qtransform(quat, p);
         
    float de = 1.0;
    de =  min(de,mapArm1(p1));
    de = smin(de,mapArm2(p2),0.04);
    de = smin(de,mapHand(p3),0.03);
    de =  min(de,mapBall(p3,s));
        
    return de;
}

float mapDragon(in vec3 p)
{
    p.zx *= rotate(PIH);
    p.yx *= rotate(0.2);
    float de = 1.0;
    de =  min(de,mapBody(p));
    de = smin(de,mapHead(p),0.03);
    de = smin(de,mapArms(p),0.06);
    return de;
}

float mapGround(in vec3 p)
{
    float de = p.y-0.2;
    de -= 0.02*sin(5.0*p.x+time*0.5);
    de -= 0.02*sin(7.0*p.z+time*1.5);
    de -= 0.02*sin(8.0*length(p.zx)+time);
    return de;
}

vec3 doMotion(in vec3 p)
{
    float t = mod(time,60.0);
    float h;
    if (t < 40.0)
    {
    	// 0-10s
    	p -= vec3(0.0,-0.5,-0.5);
    	h = clamp(t-0.0,0.0,10.0)/10.0;
    	p -= mix(vec3(0.0),vec3(0.0,0.9,0.0),h);    
    	//10-20s
    	h = clamp(t-10.0,0.0,10.0)/10.0;
    	p -= mix(vec3(0.0),vec3(0.8,-0.3,0.0),h*h);
    	p.xz *= rotate(PIH * h*h);
    	p.zy *= rotate(0.3* h);
    	//20-30s
    	h = clamp(t-20.0,0.0,10.0)/10.0;
    	p -= mix(vec3(0.0),vec3(-0.2,1.0,0.0),h*h);
    	p.zy *= rotate(-0.5* h);
    	//30-40s
    	h = clamp(t-30.0,0.0,10.0)/10.0;
    	p -= mix(vec3(0.0),vec3(-0.5,-2.3,0.0),h*h);
		p.zy *= rotate(-0.6* h*h);
    }
    else if (t < 50.0)    
    {
    	//40-50s
        p -= vec3(0.0,-2.1,0.7);
        p.zy *= rotate(PIH);        
    	h = clamp(t-40.0,0.0,10.0)/10.0;
    	p -= mix(vec3(0.0),vec3(0.5,0.0,6.0),h*h);
    	p.yx *= rotate(-PI2*1.5*h);
    }
    else
    {
    	//50-60s
    	p -= vec3(0.0,0.15,-4.5);
    	h = clamp(t-50.0,0.0,10.0)/10.0;
    	p -= mix(vec3(0.0),vec3(0.0,0.0,7.5),h);
    }
    return p;
}

float map(in vec3 p)
{   
    return min(mapGround(p),mapDragon(doMotion(p)));
}

/*
vec3 doColor(in vec3 p)　について
これは、オブジェクトの色を返すfunction。
もう少し良いfanction名が、ないか思案中。
今回のshaderを書いてる間に一番Niceと思えたアイディアが入ってます。
この色を取得するfunctionは、raymarchingの仕組み上、一番厄介な所。
みんな、手を変え品を変えと試行錯誤しています。
map関数(DEを格納してる関数)は、一番アクセスする回数が多い関数なのでコンパクトにしたい。
だから余分な情報（色など）を入れたくない。
でも入れないと同じことを別functonに書かなければならないしメンテナンスも良くない。
今回chromeがクラッシュして、何とかならないかと考えてる時に閃いた。
色を取得するfunctionが必要な時には、既にrayが当たった後、
総当たりで、mapのDEを比べて最小のDEを見つけなくても、
DEの衝突判定に使う数値より下ならば、その色だって決め打ちreturnする方法を思いついた。
const float e = 0.001;
if (mapGround(p)<e) return vec3(0.2,0.2,0.35);
こんな感じ。
ユニークな色の中でrayが当たる確率が高そうな順に書いていけば良い。
最後にベースの色をreturn。
条件付けしたエリアでのreturnもOK。
bounding boxの後ろに書くmap関数を外に出せば、bounding boxもスルー出来る。
これは、使い勝手が良い。
(2016/2/24)
*/    
vec3 doColor(in vec3 p)
{
    const float e = 0.001;
    if (mapGround(p)<e) return vec3(0.2,0.2,0.35);
    p = doMotion(p);
    p.zx *= rotate(PIH);
    p.yx *= rotate(0.2);
    vec3 q = p;
    
    // body
    p.zy -= bodyCurve(p.x);
	vec2 g = (bodyCurve(p.x+EPS)-bodyCurve(p.x-EPS))/(2.0*EPS);
    p.zy *= cos(atan(g));
    float a = atan(p.z,-p.y);
    if (abs(a)<0.65 && p.x>-1.95) return vec3(0.75,0.65,0.5);
    vec2 sg = segment(p.xy,TAIL,HEAD);
    float t = -abs(sg.y-0.6)*1.2+0.75;
    t = 0.1*smoothstep(0.0,0.25,t*t);
    if (p.y>t+0.03 && p.x>-1.8) return vec3(0.7,0.1,0.1);
    
    // arms
    p = q;
    const float offset = -1.2;
    p.x -= offset;
    p.zy -= bodyCurve(offset);
	float s = sign(p.z);   
    p.z = abs(p.z);
    vec4 quat;
	p -= vec3(0.0, 0.0, 0.15);
    quat = quaternion(normalize(vec3(0.5,1.0,0.0)),-0.7);
    p -= qtransform(invq(quat),vec3(0.0,0.0,0.3));
    quat = quaternion(normalize(vec3(-0.7,1.0,0.0)),1.0);
    p -= qtransform(invq(quat),vec3(0.0,0.0,0.3));
    quat = quaternion(normalize(vec3(0.0,0.0,1.0)),0.5);
    p = qtransform(quat, p);
    if (mapClaw(p)<e) return vec3(1.0);
    if (mapBall(p,s)<e) return vec3(0.9,0.8,0.3);

    // head
    p = q;
    p.x -= HEAD;
    p.zy -= bodyCurve(HEAD);
    if (mapHair(p)<e) return vec3(0.1,0.05,0.05);
	if (mapHorns(p)<e) return vec3(0.9,0.5,0.1);
    if (mapUpperFang(p)<e) return vec3(0.8);
    if (mapBarb(p)<e) return vec3(0.1);
    if (mapEyes(p)<e)
    {
        p -= vec3(-0.115,0.065,0.0);
    	p.z = abs(p.z); p.z -= 0.035;
        vec3 col = mix(vec3(0.05,0.2,0.35),vec3(1.0),step(0.4,length(50.0*p.yz)));
        vec2 c = vec2(0.0,0.007);
        col = mix(vec3(1.0,1.0,0.0),col,            
            step(0.4,max(length(50.0*(p.yz-c)),length(50.0*(p.yz+c)))));
        return col;
    }
    p.y -= -0.01;    
    p.xy *= rotate(0.1*sin(time)+0.3);
    if (mapBottomFang(p)<e) return vec3(0.8);

    return vec3(0.15,0.3,0.25);
}

/*
vec3 calcNormal(in vec3 pos) について
これは、raymaching必須の関数。
vec3 calcNormal(in vec3 p)
{
	const vec2 e = vec2(0.0001, 0.0);
	return normalize(vec3(
		map(p + e.xyy) - map(p - e.xyy),
		map(p + e.yxy) - map(p - e.yxy),
		map(p + e.yyx) - map(p - e.yyx)));
}
こちらの方が有名ですが、最近shadertoyでは、下の関数が流行？しています。
map関数へのアクセス数が少ないのは魅力です。
vec2 e = vec2(1.0,-1.0)*0.001;
の0.001をiqは色々変えていますが、意図が全然わかりません。
でも、大丈夫そうだから良いやで使ってます。
(2016/2/24)
*/    
vec3 calcNormal(in vec3 pos)
{
    vec2 e = vec2(1.0,-1.0)*0.001;
    return normalize(
        e.xyy*map(pos+e.xyy)+e.yyx*map(pos+e.yyx)+ 
        e.yxy*map(pos+e.yxy)+e.xxx*map(pos+e.xxx));
}

void main()
{
    vec2 p2 = (2.0*gl_FragCoord.xy-resolution.xy)/resolution.y;
    vec3 col = vec3(0.8,0.8,0.9)*(1.0-0.7*p2.y*p2.y);
    vec3 rd = normalize(vec3(p2,2.0));    
    vec3 ro = vec3(0.0,0.8,2.5);  
    ro.xz *= rotate(PI*0.77777*floor(time/60.0));
    vec3 ta = vec3(0.0,0.2,0.0);
    ro += 0.03*sin(2.0*time*vec3(1.1,1.2,1.3));
	ta += 0.03*sin(2.0*time*vec3(1.7,1.5,1.6));
    rd = lookat(ro,ta)*rd;
    float maxd = 10.0;
    float t = 0.0,d;
    for(int i=0;i<100;i++)
    {
        t += d = map(ro+rd*t);
        if(d<0.001||t>maxd) break;
    }
    if(t<maxd)
    {
        vec3 p = ro+t*rd;
        vec3 nor = calcNormal(p);
		col = doColor(p);
        vec3 li = normalize(vec3(2.0,3.0,3.0));
        float dif = clamp(dot(nor,li),0.0,1.0);
        float amb = max(0.5+0.5*nor.y,0.0);
        float spc = pow(clamp(dot(reflect(rd, nor),li),0.0,1.0),10.0);
        col *= clamp(0.0,1.0,max(dif,0.3)*amb+spc);     
        /*
        fogについて
        ふつうfogって言えば。奥行のtとガウス関数を使って
        col = mix(vec3(0.6,0.6,0.7),col,exp(-t*t*0.02));
        って感じ。
        今回は、地面からの距離も使ってみた。      
        地面をsin()で歪ませただけなのに、良い感じで雲っぽくなってくれた。
        コスト安くても良いエフェクトは、あるもんだね。
        地面からの距離の部分に min(p.y+0.25,1.0) を使ってる。
        何故、 min()か？なのだが、
        mix()の３項目の引数の範囲が0.0<=t<=1.0なので1.0以上をカットしてます。
        本来はclump(p.y+0.25, 0.0, 1.0)が王道なのだが、地面より下は見えないので少しだけコストカットしました。
        いつもmix()を使ってて思うのは、引数の範囲が0.0<=t<=1.0が絶妙だという事。
        この範囲同士の乗算はこの範囲を超えない。ものすごく使い勝手がいい。
        RGBとかも、この範囲だし、作るfunctionも自然と、この範囲を意識したものになっていく。
        きっとGLSLの設計した人間のおかげだね。
        あと、ガウス関数の返値の範囲もこの範囲でした。
        
        話は変わるが、この２D部分に現れるcolorを mix() で処理するのに悩まされた経験がある。
        なので、それについて書いてみます。
        col = mix(vec3(0.6,0.6,0.7), col, exp(-t*t*0.02));
        col = mix(col, vec3(0.6,0.6,0.7), 1.0 - exp(-t*t*0.02));
        上の２つは、同じ挙動をします。
        こうやって書いてみると、col と　vec3(0.6,0.6,0.7) の順番が入れ替わってるから、当たり前と思う。
        最初、２番目の文だけを見たときに、何故1.0から引くんだ？　結局わからず挫折しました。
        col = mix(vec3(0.6,0.6,0.7), col, step(0.01, p.x));
        col = mix(col, vec3(0.6,0.6,0.7), 1.0 - step(0.01, p.x));
        col = mix(col, vec3(0.6,0.6,0.7), smoothstep(0.01, 0.0099999, p.x));
        この3つもほぼ同じ挙動をします。
        これにも悩まされました。smoothstep()の１項目より２項目をわずかに小さくすると
        1.0 - step(0.01, p.x) とほぼ同等になります。
        そういう事みたいです。
        これらは、mix()の１項目にcolを入れた方が、ソースが読みすくなるskillなのだと気づくのに時間がかかりました。
        きっと、知っていた方がいいと思うよ。
        
        １つGLSLの仕様で不満なのは、clump()とsmoothstep()の引数に範囲を入れる位置が違う事です。
        よく間違えます。
        でも、上の文章を見ていたらsmoothstep()はstep()の派生だから、しょうがないかと今、思いました。
        まあ、一生変わらないと思うので慣れるしかないでしょう。
        (2016/2/25)        
        */
        col = mix(vec3(0.6,0.6,0.7),col,gaussian(t*0.15)*min(p.y+0.25,1.0));
		col = pow(col,vec3(0.7));
    }
    gl_FragColor = vec4(col,1.0);
}
</script>

<script id="vs" type="x-shader/x-vertex">
  attribute vec2 p;
  void main() {
    gl_Position = vec4(p,0.0,1.0);
  }
</script>

<canvas id="c"></canvas>
<div id="div0" style="color:white; font-size:12px; position:absolute; top:10px; left:10px;"></div>
<div id="div1" style="color:white; font-size:12px; position:absolute; top:23px; left:10px;"></div>
