<!DOCTYPE html>
<!--
Copyright (c) 2017 gam0022
Released under the MIT license
http://opensource.org/licenses/mit-license.php
-->
<html lang="en">
	<head>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<title>three.js webgl - raymarching - carbon</title>
		<style type="text/css">
			body {
				background-color: black;
				margin: 0;
				padding: 0;
			}

			a { color: skyblue }

			canvas {
				display: block;
				position: absolute;
				top: 0;
				left: 0;
				right: 0;
				bottom: 0;
				margin: auto;
			}

			#info {
				color: white;
				font-size: 13px;
				position: absolute;
				bottom: 10px;
				width: 100%;
				text-align: center;
				z-index: 100;
			}
		</style>
	</head>
	<body>

		<div id="info">
			<a href="http://threejs.org" target="_blank">three.js</a> - webgl raymarching example - carbon by <a href="https://github.com/gam0022" target="_blank">gam0022</a><br>
			Third place prize works in GLSL Compo, Tokyo Demo Fest 2016 (<a href="http://gam0022.net/blog/2016/02/24/tokyo-demo-fest/" target="_blank">article in Japanese</a>)
		</div>

		<script id="fragment_shader" type="x-shader/x-fragment">

#ifdef GL_ES
precision mediump float;
#endif

// uniforms
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

// debug for camera
uniform bool debugCamera;
uniform vec3 cameraPos;
uniform vec3 cameraDir;

// consts
const float INF = 1e+10;
const float EPS = 1e-2;
const float OFFSET = EPS * 100.0;

const float PI = 3.14159265359;
const float PI2 = 6.28318530718;
const float PIH = 1.57079632679;
const float PIQ = 0.78539816339;


// globals
const vec3 lightDir = vec3( -0.48666426339228763, 0.8111071056538127, -0.3244428422615251 );

vec3 cPos, cDir, target;

vec3 illuminationColor;
float tempo;

// ray
struct Ray {
	vec3 origin;
	vec3 direction;
};

#define METAL_MATERIAL   0
#define MIRROR_MATERIAL  1


// util
float hash(vec2 p) { return fract(1e4 * sin(17.0 * p.x + p.y * 0.1) * (0.1 + abs(sin(p.y * 13.0 + p.x)))); }

float noise(vec2 x) {
	vec2 i = floor(x), f = fract(x);

	float a = hash(i);
	float b = hash(i + vec2(1.0, 0.0));
	float c = hash(i + vec2(0.0, 1.0));
	float d = hash(i + vec2(1.0, 1.0));

	vec2 u = f * f * (3.0 - 2.0 * f);
	return mix(a, b, u.x) + (c - a) * u.y * (1.0 - u.x) + (d - b) * u.x * u.y;
}

float fbm(vec2 p) {
	const mat2 m2 = mat2(0.8, -0.6, 0.6, 0.8);

	p.xy += time;

	float f = 0.5000 * noise(p); p = m2 * p * 2.02;
	f += 0.2500 * noise(p); p = m2 * p * 2.03;
	f += 0.1250 * noise(p); p = m2 * p * 2.01;
	f += 0.0625 * noise(p);
	return f / 0.9375;
}

// intersection
struct Intersection {
	bool hit;
	vec3 position;
	float distance;
	vec3 normal;
	vec2 uv;
	float count;

	int material;
	vec3 color;
	float reflection;
};


// distance functions
vec3 opRep( vec3 p, float interval ) {
	return mod( p, interval ) - 0.5 * interval;
}

vec2 opRep( vec2 p, float interval ) {
	return mod( p, interval ) - 0.5 * interval;
}

float opRep( float p, float interval ) {
	return mod( p, interval ) - 0.5 * interval;
}

vec2 opRepLimit(vec2 p, float interval, float limit) {
	return mod(clamp(p, -limit, limit), interval) - 0.5 * interval;
}

// https://www.shadertoy.com/view/Mlf3Wj
mat2 rotate(in float a) {
    float s=sin(a),c=cos(a);
    return mat2(c,s,-s,c);
}

vec2 fold(in vec2 p, in float s) {
    float a = PI / s - atan(p.x, p.y);
    float n = PI2 / s;
    a = floor(a / n) * n;
    p *= rotate(a);
    return p;
}

float smin(float d1, float d2, float k){
	float h = exp(-k * d1) + exp(-k * d2);
	return -log(h) / k;
}

// thanks to https://www.shadertoy.com/view/MdVGRc
float dMbox( vec3 p ) {
  const float scale = 2.7;
  const int n = 12;
  vec4 q0 = vec4 (p, 1.);
  vec4 q = q0;

  for ( int i = 0; i < n; i++ ) {

    q.xyz = clamp( q.xyz, -1.0, 1.0 ) * 2.0 - q.xyz;
    q = q * scale / clamp( dot( q.xyz, q.xyz ), 0.5, 1.0 ) + q0;

  }

  return length( q.xyz ) / abs( q.w );
}

float sdBox( vec3 p, vec3 b ) {
  vec3 d = abs(p) - b;
  return min(max(d.x,max(d.y,d.z)),0.0) + length(max(d,0.0));
}

float dSphere(vec3 p, float r) {
	return length(p) - r;
}

float dBar( vec2 p, float interval, float width) {
	return length( max( abs( opRep( p, interval ) ) - width, 0.0 ) );
}


#define META_SCENE_END_TIME (32.0)
#define UFO_SCENE_END_TIME  (META_SCENE_END_TIME + 16.0)

float dFract(inout vec3 p) {
	float radius = 1.0;
	float radiusScale = 0.45 * abs(sin(0.3 * time));
	float d = sdBox(p, vec3(radius));
	for (int i = 0; i < 5; i++) {
		vec3 q = abs(p) + normalize(vec3(-1.0)) * radius * (1.0 + radiusScale);
		d = min(d, sdBox(p, vec3(radius)));
		p = q;
		radius *= radiusScale;
	}
	return d;
}

float dTree(vec3 p) {
	float scale = 0.6;
	float s = mix(0.2, 0.0, clamp(p.y, 0.0, 1.0));
	vec3 size = vec3(s, 1.0, s);
	float d = sdBox(p, size);
	for (int i = 0; i < 7; i++) {
		vec3 q = p;
		q.x = abs(q.x);
		q.y -= 0.5 * size.y;
		q.xy *= rotate(-1.2);
		d = min(d, sdBox(p, size));
		p = q;
		size *= scale;
	}
	return d;
}

float dSnowCrystal(inout vec3 p) {
	p.xy = fold(p.xy, 6.0);
	return dTree(p);
}

float dMix(inout vec3 p) {
	float sphere = dSphere(p, 1.5);
	float box = sdBox(p, vec3(1.1));
	float d = mix(sphere, box, 0.5 + clamp(sin(PIQ * time), -0.5, 0.5));
	return d;
}

float dMeta(inout vec3 p) {
	float a = 1.0 + cos(time * 0.3);
	float b = 0.2 * sin(time);
	float d1 = dSphere(p - target - vec3(a, 0, b), 1.0);
	float d2 = dSphere(p - target + vec3(a, 0, b), 1.0);
	float d = smin(d1, d2, 1.0);
	return d;
}

float gauss(float x) {
	float a = 50.0;
	return exp(-x * x / a);
}

float dWing(in vec3 p) {
	float t = time - META_SCENE_END_TIME;
	float l = length(p.xz);
	float a = 0.1 + 0.06 * (1.0 + sin(PI * t + l));
	float b = min(0.2 * t, 10.0) * gauss(l);
	p.y += -b + 15.0;

	vec3 p1 = p;
	p1.xz = opRepLimit(p.xz, 1.0, 20.0);

	vec3 p2 = p;
	p2 = opRep(p, 0.5);

	float d =   sdBox(p1, vec3(0.2 + a * 3.0, 12.0 - a,       0.2 + a));
	d = min(d,  sdBox(p1, vec3(0.4 - a,       13.0 - 4.0 * a, 0.1 + a)));
	d = max(d, -sdBox(p1, vec3(0.3 - a,       14.0 - 4.0 * a, a)));
	d = max(d, -sdBox(p2, vec3(0.8 * a, 1.0 - a, 0.8 * a)));
	return d;
}

float easeInCubic( float t ) {
	return t * t * t;
}

float easeInOutCubic( float t ) {
	return t < 0.5 ? 4.0 * t * t * t : ( t - 1.0 ) * ( 2.0 * t - 2.0 ) * ( 2.0 * t - 2.0 ) + 1.0;
}

float dUfo(inout vec3 p) {
	float t = max((time - META_SCENE_END_TIME - 10.0) * 0.5, 1.0);
	float t1 = floor(t);
	float t2 = t1 + easeInOutCubic(t - t1);

	p.xz = fold(p.xz, min(t2, 10.0));
	p.z -= 0.5;

	return dWing(p);
}

float dScene( vec3 p ) {
	if (time < META_SCENE_END_TIME + 20.0) {
		float d = dMeta(p);
		d = smin(d, dUfo(p), clamp((time - META_SCENE_END_TIME) * 3.0, 1.0, 10.0));
		return d;
	} else if (time <= 130.0) {
		return dUfo(p);
	} else {
		//return dFract(p);
		return dSnowCrystal(p);
	}
}


// color functions
vec3 hsv2rgb( vec3 c ) {
	vec4 K = vec4( 1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0 );
	vec3 p = abs( fract( c.xxx + K.xyz ) * 6.0 - K.www );
	return c.z * mix( K.xxx, clamp( p - K.xxx, 0.0, 1.0 ), c.y );
}

// thanks to http://glslsandbox.com/e#21290.5
vec2 circuitPattern( vec2 p ) {

	p = fract(p);
	float r = 0.123;
	float v = 0.0, g = 0.0;
	r = fract(r * 9184.928);
	float cp, d;
	
	d = p.x;
	g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 1000.0);
	d = p.y;
	g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 1000.0);
	d = p.x - 1.0;
	g += pow(clamp(3.0 - abs(d), 0.0, 1.0), 1000.0);
	d = p.y - 1.0;
	g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 10000.0);
	
	const int iter = 15;
	for(int i = 0; i < iter; i ++)
	{
		cp = 0.5 + (r - 0.5) * 0.9;
		d = p.x - cp;
		g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 200.0);
		if(d > 0.0) {
			r = fract(r * 4829.013 * 2.0);
			p.x = (p.x - cp) / (1.0 - cp);
			v += 1.0;
		}
		else {
			r = fract(r * 1239.528);
			p.x = p.x / cp;
		}
		p = p.yx;
	}
	v /= float(iter);
	return vec2(g, v);

}

vec3 calcNormal( vec3 p ) {
	vec2 e = vec2( 1.0, -1.0 ) * 0.001;
	return normalize(
		e.xyy * dScene( p + e.xyy ) + e.yyx * dScene( p + e.yyx ) + 
			e.yxy * dScene( p + e.yxy ) + e.xxx * dScene( p + e.xxx ) );
}

void setMaterialAndUv(inout Intersection intersection, vec3 p) {
	intersection.material = METAL_MATERIAL;

	//if ( false && time <= META_SCENE_END_TIME ) {
	//	intersection.material = MIRROR_MATERIAL;
	//} else {
	//	dUfo(p);
	//	intersection.material = METAL_MATERIAL;
	//	intersection.uv = p.xz;
	//}
}

void intersectScene( inout Intersection intersection, inout Ray ray ) {
	float d;
	intersection.distance = 0.0;
	vec3 p = ray.origin;

	for ( float i = 0.0; i < 128.0; i++ ) {
		d = dScene( p );
		intersection.distance += d;
		p = ray.origin + intersection.distance * ray.direction;
		intersection.count = i;
		if (abs(d) < EPS || intersection.distance > 100.0) break;
	}

	if ( abs( d ) < EPS ) {
		intersection.hit = true;
		intersection.position = p;
		intersection.normal = calcNormal( p );
		setMaterialAndUv( intersection, p );
	}
}

float calcAo(in vec3 p, in vec3 n){
	float sca = 1.0, occ = 0.0;
	for(float i=0.; i<5.; i++){
		float hr = 0.05 + i * 0.08;
		float dd = dScene(n * hr + p);
		occ += (hr - dd) * sca;
		sca *= 0.5;
	}
	return clamp(1.0 - occ, 0.0, 1.0);
}

float calcShadow(in vec3 p, in vec3 rd) {
	float d;
	float distance = OFFSET;
	float bright = 1.0;
	float shadowIntensity = 0.5;
	float shadowSharpness = 16.0;

	for (int i = 0; i < 30; i++) {
		d = dScene(p + rd * distance);
		if (d < EPS) return shadowIntensity;
		bright = min(bright, shadowSharpness * d / distance);
		distance += d;
	}

	return shadowIntensity + (1.0 - shadowIntensity) * bright;
}

void calcRadiance( inout Intersection intersection, inout Ray ray, int bounce ) {
	intersection.hit = false;
	intersectScene( intersection, ray );

	if ( intersection.hit ) {
		float diffuse = clamp(dot(lightDir, intersection.normal), 0.2, 1.0) * 0.5 + 0.5;
		float specular = pow(clamp(dot(reflect(lightDir, intersection.normal), ray.direction), 0.0, 1.0), 10.0);
		float ao = calcAo(intersection.position, intersection.normal);
		float shadow = calcShadow(intersection.position, lightDir);

		if ( intersection.material == METAL_MATERIAL ) {
			//intersection.color = vec3( 0.2 ) + 0.2 * diffuse + specular - 0.01 * float( intersection.count );
			//vec2 uv = intersection.uv;
			//vec2 dg = circuitPattern( uv );
			//float glow = max( sin( length( uv ) - 1.8 * time ) * 2.5, 0.0 );
			//float pattern = max( dg.x - 1.0, 0.0 );
			//intersection.color = vec3(0.2) + 0.5 * diffuse * ao + 0.1 * specular;
			intersection.color = hsv2rgb(vec3(0.1 * intersection.count, 0.3 + 0.3 * sin(time), 0.5)) * diffuse * ao * shadow + 0.1 * specular;
			intersection.reflection = 0.7;
		} else {
			intersection.color = vec3(0.0);
			intersection.reflection = 1.0;
		}

		intersection.color = mix(intersection.color, 0.8 * vec3(0.7, 0.75, 0.8), min(1.0, pow(0.02 * intersection.distance, 2.0)));
	} else {
		vec3 base = vec3(0.4, 0.55, 0.8) * (1.0 - 0.8 * ray.direction.y) * 0.9;
		vec3 cloud = vec3(1.0, 0.95, 1.0);

		// Sun
		float sundot = clamp(dot(ray.direction, lightDir), 0.0, 1.0);
		base += 0.25 * vec3(1.0, 0.7, 0.4) * pow(sundot, 8.0);
		base += 0.75 * vec3(1.0, 0.8, 0.5) * pow(sundot, 64.0);

		// Clouds
		float rd = ray.direction.y + 0.5;
		intersection.color = mix(base, cloud, 0.5 *
			smoothstep(0.5, 0.8, fbm((ray.origin.xz + ray.direction.xz * (250000.0 - ray.origin.y) / rd) * 0.000008)));

		intersection.color = mix(intersection.color, vec3(0.7, 0.75, 0.8), pow(1.0 - max(rd, 0.0), 4.0));
	}

	//intersection.color = mix(intersection.color, vec3(1.0), clamp(0.1 * (time - 120.0), 0.0, 1.0));
}

// gaz
mat3 lookat(in vec3 eye, in vec3 target) {
	vec3 w = normalize(target-eye);
	vec3 u = normalize(cross(w,vec3(0.0,1.0,0.0)));
	vec3 v = normalize(cross(u,w));
    return mat3(u,v,w);
}

void setupCamera() {
	// time
	float t1 = time - META_SCENE_END_TIME;
	float t2 = t1 - 30.0;
	float t3 = t2 - 16.0;
	float t4 = t3 - 30.0;

	// blend
	float b1 = easeInOutCubic(clamp(t1 * 0.2, 0.0, 1.0));
	float b2 = easeInOutCubic(clamp(t2 * 0.4, 0.0, 1.0));
	float b3 = easeInOutCubic(clamp(t3 * 0.4, 0.0, 1.0));
	float b4 = easeInOutCubic(clamp(t4 * 0.2, 0.0, 1.0));

	target = vec3(0.0);
	target.y = 0.7 * clamp(META_SCENE_END_TIME - time, -4.0, 40.0);
	target.y = mix(target.y, 1.0, step(1.0, t2));

	// position
	vec3 p1 = target + vec3(0.0, 0.0, -5.0);

	float a = 0.2 * (time - META_SCENE_END_TIME);
	vec3 p2 = vec3(1.0 * pow(0.1, a), abs(8.0 * cos(0.1 * time)), 1.0 * pow(0.1, a));

	vec3 p3 = vec3(4.0, 1.0 + t2 * 0.4, 0.0);
	vec3 p4 = vec3(0.01, 4.0 + t3 * 0.2, 0.0);
	vec3 p5 = vec3(1.5, 10.0 + t4 * 0.1, 1.0);

	cPos = mix(p1, p2, b1);
	cPos = mix(cPos, p3, b2);
	cPos = mix(cPos, p4, b3);
	cPos = mix(cPos, p5, b4);

	// direction
	vec3 d1 = normalize(target - cPos);
	vec3 d2 = vec3(-0.2, -1.0, -0.2);

	cDir = mix(d1, d2, b1);
	cDir = mix(cDir, d1, b2);
}

void main(void) {
	// fragment position
	vec2 fragPos = ( gl_FragCoord.xy * 2.0 - resolution ) / min( resolution.x, resolution.y );

	// camera and ray
	Ray ray;

	if ( debugCamera ) {
		cPos  = cameraPos;
		cDir  = cameraDir;
	} else {
		setupCamera();
	}

	float targetDepth = 1.3;
	vec3 up =  vec3( 0.0, 1.0 ,0.0 );
	vec3 cSide = normalize( cross( cDir, up ) );
	vec3 cUp   = normalize( cross( cSide, cDir ) );
	ray.origin = cPos;
	ray.direction = normalize( cSide * fragPos.x + cUp * fragPos.y + cDir * targetDepth );

	// music's tempo
	tempo = sin( PI * time );
	//tempo = sin( PI * time ) + sin( 3.0 * PI * time ) + sin( 2.0 * PI * time ) + sin( PI * time );
	//tempo /= 4.0;

	// Illumination Color
	illuminationColor = hsv2rgb( vec3( time * 0.02 + 0.6, 1.0, 1.0 ) );

	vec3 color = vec3( 0.0 );
	float reflection = 1.0;
	Intersection intersection;

	for ( int bounce = 0; bounce <= 2; bounce++ ) {
		calcRadiance( intersection, ray, bounce );

		color += reflection * intersection.color;
		if ( !intersection.hit /* || intersection.material != METAL_MATERIAL*/ ) break;

		reflection *= intersection.reflection;
		ray.origin = intersection.position + intersection.normal * OFFSET;
		ray.direction = normalize(reflect(ray.direction, intersection.normal));
	}

	//color += 0.2 * tempo;

	gl_FragColor = vec4( color, 1.0 );

}

		</script>

		<script id="vertex_shader" type="x-shader/x-vertex">

			attribute vec3 position;

			void main(void) {

				gl_Position = vec4(position, 1.0);

			}

		</script>

		<script src="js/three.min.js"></script>
		<script src="js/controls/FlyControls.js"></script>
		<script src="js/controls/OrbitControls.js"></script>

		<script src="js/libs/stats.min.js"></script>
		<script src="js/libs/dat.gui.min.js"></script>

		<script>

			var camera, scene, flyControls, orbitControls, renderer;
			var geometry, material, plane;
			var mouse = new THREE.Vector2( 0.5, 0.5 );
			var canvas;
			var stats;

			var clock = new THREE.Clock();
			var offsetTime = 150.0;

			var config = {
				saveImage: function() {

					renderer.render( scene, camera );
					window.open( canvas.toDataURL() );

				},
				camera: 'Orbit',
				resolution: '512',
			};

			init();
			render();

			function init() {

				scene = new THREE.Scene();
				camera = new THREE.PerspectiveCamera(35, 800/600);
				camera.position.z = 5;
				camera.lookAt( new THREE.Vector3( 0.0, 0.0, 0.0 ) );

				geometry = new THREE.PlaneBufferGeometry( 2.0, 2.0 );
				material = new THREE.RawShaderMaterial( {
					uniforms: {
						resolution: { type: 'v2', value: new THREE.Vector2( config.resolution, config.resolution ) },
						mouse: { type: 'v2', value: mouse },
						time: { type: 'f', value: 0.0 },
						debugCamera: { type: 'i', value: config.camera !== 'GLSL' },
						cameraPos: { type: 'v3', value: camera.getWorldPosition() },
						cameraDir: { type: 'v3', value: camera.getWorldDirection() },
					},
					vertexShader: document.getElementById( 'vertex_shader' ).textContent,
					fragmentShader: document.getElementById( 'fragment_shader' ).textContent
				} );
				plane = new THREE.Mesh( geometry, material );
				plane.frustumCulled = false;
				scene.add( plane );

				renderer = new THREE.WebGLRenderer();
				//renderer.setPixelRatio( window.devicePixelRatio );
				renderer.setSize( config.resolution, config.resolution );

				canvas = renderer.domElement;
				//canvas.addEventListener( 'mousemove', onMouseMove );
				window.addEventListener( 'resize', onWindowResize );
				document.body.appendChild( canvas );

				//flyControls = new THREE.FlyControls( camera, canvas );
				//flyControls.autoForward = true;
				//flyControls.dragToLook = false;
				//flyControls.rollSpeed = Math.PI / 12;
				//flyControls.movementSpeed = 0.1;

				orbitControls = new THREE.OrbitControls( camera, canvas );
				orbitControls.enablePan = true;
				//orbitControls.keyPanSpeed = 0.01;
				orbitControls.enableDamping = false;
				//orbitControls.dampingFactor = 0.015;
				orbitControls.enableZoom = true;
				//orbitControls.zoomSpeed = 0.001;
				//orbitControls.rotateSpeed = 0.8;
				orbitControls.autoRotate = false;
				orbitControls.autoRotateSpeed = 0.0;
				orbitControls.target = new THREE.Vector3( 0.0, 0.0, 0.0 );

				var gui = new dat.GUI();
				gui.add( config, 'saveImage' ).name( 'Save Image' );
				gui.add( config, 'camera', [ 'GLSL', 'Orbit', /*'Fly'*/ ] ).name( 'Camera' );
				gui.add( config, 'resolution', [ '256', '512', '800', 'full' ] ).name( 'Resolution' ).onChange( function( value ) {

					if ( value !== 'full' ) {

						canvas.width = value;
						canvas.height = value;

					}
					onWindowResize();

				} );

				stats = new Stats();
				document.body.appendChild( stats.domElement );

			}

			function render( timestamp ) {

				var delta = clock.getDelta();

				stats.begin();

				if ( config.camera === "Fly" ) {

					flyControls.update( delta );

				} else if ( config.camera === "Orbit" ) {

					orbitControls.update();

				} else {

					//camera.position.set( mouse.x - 0.5, mouse.y * 4, timestamp );
					//camera.lookAt( new THREE.Vector3( 0.0, -0.3, 1.0 ).add( camera.position ) );

				}

				//if ( camera.position.y < 0 ) camera.position.y = 0;

				material.uniforms.resolution.value = new THREE.Vector2( canvas.width, canvas.height );
				material.uniforms.mouse.value = mouse;
				material.uniforms.time.value = offsetTime + timestamp * 0.001;
				//material.uniforms.time.value = 147.81;
				//material.uniforms.time.value = 62.68;
				//material.uniforms.time.value = 93.69;
				//material.uniforms.time.value = 67.86;
				material.uniforms.debugCamera.value = config.camera !== 'GLSL';
				material.uniforms.cameraPos.value = camera.getWorldPosition();
				material.uniforms.cameraDir.value = camera.getWorldDirection();
				renderer.render( scene, camera );

				stats.end();
				requestAnimationFrame( render );

				var pos = camera.getWorldPosition();
				var dir = camera.getWorldDirection();

				document.getElementById( 'info' ).innerHTML = ""
					+ pos.x + ", " + pos.y + ", " + pos.z + "<br />"
					+ dir.x + ", " + dir.y + ", " + dir.z + "<br />"
					+ material.uniforms.time.value;

			}

			function onMouseMove( e ) {

				mouse.x = e.offsetX / canvas.width;
				mouse.y = e.offsetY / canvas.height;

			}

			function onWindowResize( e ) {

				if ( config.resolution === 'full' ) {

					canvas.width = window.innerWidth;
					canvas.height = window.innerHeight;

				}

				renderer.setSize( canvas.width, canvas.height );

			}

		</script>

	</body>
</html>
